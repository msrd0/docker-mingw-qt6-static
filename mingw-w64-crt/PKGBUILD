# Contributor: Felix Yan <felixonmars@archlinux.org>

pkgname=mingw-w64-crt
pkgver=10.0.0
pkgrel=1
pkgdesc='MinGW-w64 CRT for Windows'
arch=('any')
url='http://mingw-w64.sourceforge.net'
license=('custom')
groups=('mingw-w64-toolchain' 'mingw-w64')
makedepends=('mingw-w64-gcc' 'mingw-w64-binutils' "mingw-w64-headers=${pkgver}")
options=('!strip' '!buildflags' 'staticlibs' '!emptydirs')
validpgpkeys=('CAF5641F74F7DFBA88AE205693BDB53CD4EBC740')
source=(
    https://sourceforge.net/projects/mingw-w64/files/mingw-w64/mingw-w64-release/mingw-w64-v${pkgver}.tar.bz2{,.sig}
    lib32-api-ms-win-core-synch-l1-2-0.def::https://raw.githubusercontent.com/mirror/mingw-w64/984233c02aa796fd5df35323f125bd062bdb3df5/mingw-w64-crt/lib32/api-ms-win-core-synch-l1-2-0.def
    lib64-api-ms-win-core-synch-l1-2-0.def::https://raw.githubusercontent.com/mirror/mingw-w64/984233c02aa796fd5df35323f125bd062bdb3df5/mingw-w64-crt/lib64/api-ms-win-core-synch-l1-2-0.def
)
sha512sums=('3c0827af7c40809a867758f6cd9ef3ff0c988b43082345bf725e03949af95968d634ace99a7ffae323189549221dcb5d861de9e801f1fbc7904e446641b60516'
            'SKIP'
            'e448628be45249132aa4e30bf44f0672fd94d3e9fa8774d1433bdb8b31d5ccee20e13953cbf022a3ca2eaef13944d92f5d74638cbbf7b85c07f0cc168690c189'
            'e17734b318a10ea1827f87297b9a3dc242d339ba482fdeafc86d6554b5f836abd7acec9483dad2dbf4b67455b254805e6bba81e02ad974afb41311a7868e27e0')

_targets="i686-w64-mingw32 x86_64-w64-mingw32"

prepare() {
  cd "$srcdir"
  for lib in lib32 lib64; do
    cp $lib-api-ms-win-core-synch-l1-2-0.def mingw-w64-v$pkgver/mingw-w64-crt/$lib/
  done
}

build() {
  cd "$srcdir"
  for _target in ${_targets}; do
    echo "Building ${_target} CRT"
    if [ ${_target} == "i686-w64-mingw32" ]; then
        _crt_configure_args="--disable-lib64 --enable-lib32"
    elif [ ${_target} == "x86_64-w64-mingw32" ]; then
        _crt_configure_args="--disable-lib32 --enable-lib64"
    fi
    mkdir -p "$srcdir"/crt-${_target} && cd "$srcdir"/crt-${_target}
    "$srcdir"/mingw-w64-v${pkgver}/mingw-w64-crt/configure --prefix=/usr/${_target} \
        --host=${_target} --enable-wildcard \
        ${_crt_configure_args}
    make
  done
}

package() {
  for _target in ${_targets}; do
    echo "Installing ${_target} crt"
    cd "$srcdir"/crt-${_target}
    make DESTDIR="$pkgdir" install
  done
}
